;int 9Â§ñ‰∏≠Êñ≠
assume cs:code

data segment
		  ;0123456789abcdef	
SCREEN		db 18 dup('1000000000000001')
		db 	  '1111111111111111'

SCREEN_ROW		dw 19
SCREEN_COL		dw 16
SCREEN_LOCATION 	dw 160*3

BLOCK0		db '1100'
		db '1100'
		db '0000'
		db '0000'

BLOCK1		db '0000'
		db '1111'
		db '0000'
		db '0000'

		db '0100'
		db '0100'
		db '0100'
		db '0100'

BLOCK_S		db '0000'
		db '0110'
		db '1100'
		db '0000'

		db '0100'
		db '0110'
		db '0010'
		db '0000'

BLOCK_Z		db '0000'
		db '1100'
		db '0110'
		db '0000'

		db '0010'
		db '0110'
		db '0100'
		db '0000'

BLOCK_T		db '0100'
		db '1110'
		db '0000'
		db '0000'

		db '0100'
		db '0110'
		db '0100'
		db '0000'

		db '0000'
		db '1110'
		db '0100'
		db '0000'

		db '0100'
		db '1100'
		db '0100'
		db '0000'

BLOCK_J 	db '0010'
		db '0010'
		db '0110'
		db '0000'

		db '0000'
		db '1000'
		db '1110'
		db '0000'

		db '0110'
		db '0100'
		db '0100'
		db '0000'

		db '0000'
		db '1110'
		db '0010'
		db '0000'

BLOCK_L		db '0100'
		db '0100'
		db '0110'
		db '0000'

		db '0000'
		db '1110'
		db '1000'
		db '0000'

		db '0110'
		db '0010'
		db '0010'
		db '0000'

		db '0000'
		db '0010'
		db '1110'
		db '0000'

BLOCK		dw OFFSET BLOCK0
		dw OFFSET BLOCK1
		dw OFFSET BLOCK_S
		dw OFFSET BLOCK_Z
		dw OFFSET BLOCK_T
		dw OFFSET BLOCK_J
		dw OFFSET BLOCK_L
					
BLOCK_ROW	dw 4
BLOCK_COL	dw 4
BLOCK_COLOR	dw 2201h	;ÁªøËâ≤Á¨ëËÑ∏üòä

LEFT		db 4bh
RIGHT		db 4Dh
DOWN		db 50h
BLANK		db 39h
isAllowMoveDown	dw 0

BLOCK_TOP 		dw 0

BLOCK_NOW_STATUS	dw 0		
BLOCK_MAX		dw 0
BLOCK_FIRST		dw 0
BLOCK_MAX_STATUS	dw 0, 1, 1, 1, 3, 3, 3 

data ends

string segment
	db 'Game over', 0
string ends

stack segment
	db 128 dup(0)
stack ends

code segment
start:
	mov ax, stack 
	mov ss, ax
	mov sp, 128

	call cpyTetris	
	call saveOldInt9
	call setNewInt9
		
		
	mov bx, 0
	push bx
	mov bx, 7e00h	
	push bx
	retf
	
	
	;call restoreOldInt9	;ÊÅ¢Â§çint9
	mov ax, 4c00h
	int 21h
;======================================
tetris:
	call initReg
	call clearScreen
	call initScreen	

nextBlock:
	cli
	call initBlockType
	call initBlockLocation
	call checkGameOver
	call printBlock
	sti

nextMoveDown:
	call delay
	cli	;Â±èËîΩÈîÆÁõòÂ§ñ‰∏≠Êñ≠	
	call isMOveDown
	cmp isAllowMoveDown, 0
	je nextBlock
	sti
	jmp nextMoveDown

gameOver:
	add sp, 12	;ËøòË¶ÅÂä†‰∏äretÁöÑpop
	call printOver
	mov ax, 4c00h
	int 21h
;========================================
delay:
	push ax
	push dx

	mov ax, 0
	mov dx ,200h
delay_:
	sub ax, 100h
	sbb dx, 0
	cmp dx, 0
	jne delay_

	pop dx
	pop ax
	ret
;========================================
printOver:
	push ax
	push bx
	push ds
	push si

	mov bx, string
	mov ds, bx
	mov bx, 0
	mov si, 160*10+80

print_over:
	mov al, ds:[bx]
	cmp al, 0
	je printOverRet
	mov es:[si], al
	inc bx
	add si, 2
	jmp print_over 
	
printOverRet:
	pop si
	pop ds
	pop bx
	pop ax
	ret
;========================================
checkGameOver:
	push bx
	push si

	mov cx, BLOCK_ROW
checkOverRow:
	push bx
	push cx
	push si
	mov cx, BLOCK_COL

checkOverCol:	
	cmp byte ptr ds:[si], '1'
	jne nCheckOverCol
	cmp word ptr es:[bx], 0030h
	jne gameOver

nCheckOverCol:
	add bx, 2
	inc si
	loop checkOverCol

	pop si
	pop cx
	pop bx
	add si, 4
	add bx, 160
	loop checkOverRow
	
	pop si
	pop bx
	ret
;========================================
printBlock:
	push si
	push bx

	mov cx, BLOCK_ROW
	mov dx, BLOCK_COLOR

printBLOCK_ROW:
	push cx
	push si
	push bx

	mov cx, BLOCK_COL

printBLOCK_COL:
	cmp byte ptr ds:[si], '1'
	jne nPrintBLOCK_COL
	mov es:[bx], dx

nPrintBLOCK_COL:
	inc si
	add bx, 2
	loop printBLOCK_COL

	pop bx
	pop si
	pop cx
	
	add bx, 160
	add si, 4
	loop printBLOCK_ROW
	
	pop bx
	pop si
	ret
;========================================
initBlockLocation:
	mov bx,  SCREEN_LOCATION
	add bx, 7*2
	ret
;========================================
;BLOCK		dw OFFSET BLOCK0
;		dw OFFSET BLOCK1
;		dw OFFSET BLOCK_S
;		dw OFFSET BLOCK_Z
;		dw OFFSET BLOCK_T
;		dw OFFSET BLOCK_J
;		dw OFFSET BLOCK_L
;Âà©Áî®CMOSÁöÑÁßí/7ÂæóÂà∞ÈöèÊú∫Êï∞
initBlockType:
	call getBlockType	
	add bx, bx
	mov si, ds:BLOCK[bx]

	mov BLOCK_ROW, 4
	mov BLOCK_COL, 4
	
	mov isAllowMoveDown, 1

	mov BLOCK_NOW_STATUS, 0
	mov BLOCK_FIRST, si
	push ds:BLOCK_MAX_STATUS[bx]
	pop BLOCK_MAX

	ret
;========================================
getBlockType:
	mov al, 0
	out 70h, al
	in al, 71h
		
	mov dl, al	;‰øùÂ≠ò‰∏™‰Ωç
	and dl, 00001111b
	mov cl, 4
	shr al, cl
		
	mov bl, 10
	mul bl
	add al, dl

	mov dx, 0
	mov bx, 7
	div bx	;dx‰∏∫‰ΩôÊï∞

	mov bx, dx	
	ret
;========================================
initScreen:
	mov si, OFFSET SCREEN
	mov di, SCREEN_LOCATION
	mov cx, SCREEN_ROW

init_screen_row:	
	push cx
	push si
	push di
	mov cx, SCREEN_COL

init_screen_col:
	mov dh, 0
	mov dl, ds:[si]
	cmp dl, '1'
	jne init_screen_col_
	mov dh, 00010001b

init_screen_col_:
	mov es:[di], dx
	inc si
	add di, 2
	loop init_screen_col

	pop di
	pop si
	pop cx	
	
	add si, 16
	add di, 160
	loop init_screen_row
	ret
;============================================
clearScreen:
	mov cx, 2000
	mov di, 0
	mov dx, 0700h
clear_screen:
	mov es:[di], dx
	add di, 2
	loop clear_screen
;============================================
initReg:
	mov bx, 0b800h
	mov es, bx
	mov bx, data
	mov ds, bx
	ret
;--------------------------------------------
newInt9:
	push ax
	in al, 60h ;‰ªé60hÁ´ØÂè£ËØªÂÖ•‰∏Ä‰∏™Â≠óËäÇ
	pushf
	call dword ptr cs:[200h]

	cmp al, 4bh
	je isLeft
	cmp al, 4dh
	je isRight
	cmp al, 50h
	je isDown
	cmp al, 39h
	je isBlank
	
	cmp al, 3bh ;	F1ÈîÆ
	jne newInt9Ret
	call changeScreenColor

newInt9Ret:
	pop ax
	iret
;==========================================
isLeft:
	;mov bx, 0b800h
	;mov es, bx
	mov di, 160*10+40
	mov byte ptr es:[di], 'L'
	call isMoveLeft
	jmp newInt9Ret

isRight:
;	mov bx, 0b800h
;	mov es, bx
	mov di, 160*10+40
	mov byte ptr es:[di], 'R'
	call isMoveRight
	jmp newInt9Ret

isDown:
;	mov bx, 0b800h
;	mov es, bx
	mov di, 160*10+40
	mov byte ptr es:[di], 'D'
;	call isMoveDown
	cmp isAllowMoveDown, 0
	je isNextBlock
	jmp newInt9Ret

isNextBlock:
	pop ax
	add sp, 4
	popf
	jmp nextBlock

isBlank:
;	mov bx, 0b800h
;	mov es, bx
	mov di, 160*10+40
	mov byte ptr es:[di], 'B'
	call isTurnBlock
	jmp newInt9Ret
;=====================================
isTurnBlock:
	push bx
	push si	
	push BLOCK_NOW_STATUS
	
	inc BLOCK_NOW_STATUS
	mov dx, BLOCK_NOW_STATUS
	cmp dx, BLOCK_MAX
	jna checkTurn	;‰∏çÈ´ò‰∫éÂàôËΩ¨Áßª
	mov si, BLOCK_FIRST
	mov BLOCK_NOW_STATUS, 0
	jmp isTurn

checkTurn:
	add si, 16

isTurn:
	push si
	mov cx, BLOCK_ROW

checkTurnBlock:
	push cx
	push bx
	push si
	mov cx, BLOCK_COL

nCheckTurnRow:
	cmp byte ptr ds:[si], '1'
	jne nCheckTurnCol
	cmp word ptr es:[bx], 2201h
	je nCheckTurnCol
	cmp word ptr es:[bx], 0030h
	jne noTurnBlock

nCheckTurnCol:
	add bx, 2
	inc si
	loop nCheckTurnRow
	
	pop si
	pop bx
	pop cx
	add si, 4
	add bx, 160
	loop checkTurnBlock

	pop si
	add sp, 4
	pop bx

	call clearOldBlock
	call printBlock
	ret

noTurnBlock:
	add sp, 8	
	pop BLOCK_NOW_STATUS
	pop si
	pop bx

	ret
;=====================================
isMoveLeft:
	push bx
	mov cx, BLOCK_ROW

moveLeft:	
	push bx
	push cx

	mov cx, BLOCK_COL

nMoveLeftRow:
	cmp word ptr es:[bx], 2201h
	jne nMoveLeftCol
	cmp word ptr es:[bx-2], 0030h
	jne noMoveLeft
	jmp nextLeftRow

nMoveLeftCol:
	add bx, 2
	
	loop nMoveLeftRow
nextLeftRow:
	pop cx
	pop bx
	add bx, 160
	loop moveLeft
	
	pop bx
        call clearOldBlock
        sub bx, 2
        call printBlock
	ret

noMoveLeft:
	add sp, 4
	pop bx	
        ret
;====================================
isMoveRight:
      	push bx
	mov cx, BLOCK_ROW

moveRight:	
	push bx
	push cx
	mov cx, BLOCK_COL

nMoveRightRow:
	cmp word ptr es:[bx], 2201h
	jne nMoveRightCol
	cmp word ptr es:[bx+2], 2201h	;'Á¨ëËÑ∏'
	je nMoveRightCol
	cmp word ptr es:[bx+2], 0030h	;'0'
	jne noMoveRight
	jmp nextRightRow
nMoveRightCol:
	add bx, 2
	loop nMoveRightRow
nextRightRow:
	pop cx
	pop bx
	add bx, 160
	loop moveRight
	
	pop bx
        call clearOldBlock
        add bx, 2
        call printBlock
	ret

noMoveRight:
	add sp, 4
	pop bx	
        ret
;====================================
isMoveDown:	
	push bx
	mov cx, BLOCK_ROW
moveDown:	
	push cx
	push bx
	mov cx, BLOCK_COL
nMoveDownRow:
	cmp word ptr es:[bx], 2201h
	jne nMoveDownCol
	cmp word ptr es:[bx+160], 2201h
	je nMoveDownCol
	cmp word ptr es:[bx+160], 0030h	;'0'
	jne noMoveDown

nMoveDownCol:
	add bx, 2
	loop nMoveDownRow

	pop  bx
	pop cx
	add bx, 160
	loop moveDown	
	
	pop bx
        call clearOldBlock
        add bx, 160
        call printBlock
	ret
	
noMoveDown:
	add sp, 4
	pop bx
        mov isAllowMoveDown, 0
	call setBottomBlock
	call getRow	;ÂæóÂà∞‰∏ÄË°åÊúÄÂ∑¶ËæπÁöÑ‰ΩçÁΩÆ
	call checkClearRow
	ret

;===============================
checkClearRow:	
	push bx
	mov cx, 4
	
checkRow:
	push bx
	push cx
	mov dx, bx
	mov cx, 14
checkCol:
	cmp word ptr es:[bx], 4432h
	jne nCheckCol
	
	add bx, 2
	loop checkCol
	
	call getTopRow
	mov bx, dx
	call clearRow
	call drawNewScreen

nCheckCol:
	pop cx
	pop bx
	add bx, 160
	loop checkRow
	
	pop bx
	ret
;===============================
clearRow:
	push bx

	mov cx, 14
clear_check_row:
	mov word ptr es:[bx], 0030h
	add bx, 2
	loop clear_check_row
	
	pop bx
	ret
;===============================
getTopRow:
	mov bx, SCREEN_LOCATION	
	add bx, 2
	mov cx, 18	;Ë°å

get_top_row:
	push bx
	push cx
	mov cx, 14
	
getTopCol:
	cmp word ptr es:[bx], 4432h
	je getTopRowRet
	add bx, 2
	loop getTopCol
	
	pop cx
	pop bx
	add bx, 160
	jmp get_top_row

getTopRowRet:
	pop cx
	pop bx
	mov BLOCK_TOP, bx	
;	mov word ptr es:[bx], 5535h
	ret
;===============================
drawNewScreen:
;	call getTopRow
;s3:
;	mov ax, 1000h
;	jmp s3
	mov bx, BLOCK_TOP
	cmp bx, dx
	je drawNewScreenRet	;Áõ∏Á≠âÂàôÁõ¥Êé•ÈÄÄÂá∫

	mov ax, dx
	sub ax, bx

	push dx
	mov dx, 0
	mov cx, 160
	div cx
	mov cx, ax
	pop dx

	mov si, dx
	sub si, 160
	mov di, dx
	
drawRow:
	push si
	push di
	push cx	
	
	mov cx, 14
drawCol:
	push es:[si]
	pop es:[di]
	add si, 2
	add di, 2
	loop drawCol

	pop cx
	pop di
	pop si	
	sub si, 160
	sub di, 160
	loop drawRow
;	mov bx, dx
;	sub bx, 160
;	sub bx, 160
	call clearRow
drawNewScreenRet:
	ret
;===============================
getRow:
	cmp byte ptr es:[bx], '1'
	je get_row
	sub bx, 2
	jmp getRow
	
get_row:
	add bx, 2
;	mov word ptr es:[bx], 5535h
	ret
;====================================
setBottomBlock:	
	push bx
	mov cx, BLOCK_ROW

setBottomRow:
	push bx
	push cx
	mov cx, BLOCK_COL
	
setBottomCol:
	cmp word ptr es:[bx], 2201h
	jne setBottomRet
	mov word ptr es:[bx], 4432h

setBottomRet:
	add bx, 2

	loop setBottomCol
	pop cx
	pop bx
	
	add bx, 160
	loop setBottomRow
	pop bx

	ret
;====================================
clearOldBlock:
        push bx
;	mov bx, SCREEN_LOCATION
        mov cx, 4
        
clearOldBlockRow:        
        
       push bx
        push cx
        mov cx, 4
        
clearOldBlockCol:         
        cmp word ptr es:[bx], 2201h
        jne nClearOldBlock
        mov word ptr es:[bx], 0030h
        
nClearOldBlock:        
        add bx, 2
        loop clearOldBlockCol
        pop cx
        pop bx
        
        add bx, 160
        loop clearOldBlockRow
        
        pop bx
        ret        
;============================================
changeScreenColor:
	push bx
	push cx
	push es
	push di

	mov bx, 0b800h
	mov es, bx
	mov cx, 2000
	mov di, 1
change_screen_color:
	inc byte ptr es:[di]
	add di, 2
	loop change_screen_color

	pop di
	pop es
	pop cx
	pop bx		
	ret
;===============================================
tetrisEnd:
	nop
;======================================

setNewInt9:
	mov bx, 0
	mov es, bx
	
	cli	
	mov word ptr es:[9*4], OFFSET newInt9 - OFFSET tetris + 7e00h
	mov word ptr es:[9*4+2], 0
	sti
	ret
;======================================
restoreOldInt9:
	mov bx, 0
	mov es, bx
	
	push  es:[200h]
	pop es:[9*4]
	push  es:[202h]
	pop es:[9*4+2]
	ret
;======================================

saveOldInt9:
	mov bx, 0
	mov es, bx
	
	push  es:[9*4]
	pop es:[200h]
	push  es:[9*4+2]
	pop es:[202h]
	ret
;======================================
cpyTetris:
	mov ax, cs
	mov ds, ax
	mov ax, 0
	mov es, ax

	mov si, OFFSET tetris
	mov di, 7e00h
	mov cx, OFFSET tetrisEnd - OFFSET tetris
	cld
	rep movsb
 
	ret

code ends

end start
